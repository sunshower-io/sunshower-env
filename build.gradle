

/**
 * Buildscript Plugins Section
 *
 * @author haswell
 */
buildscript {
    apply from: 'gradle/buildscript.gradle', to: buildscript
}


allprojects {

    group='io.hasli.test'
    version = rootProject.version
    apply plugin: 'java'

    apply plugin: 'nebula.optional-base'
    
    apply from: "$rootDir/gradle/idea.gradle"
    apply from: "$rootDir/gradle/publishing.gradle"



}

task prepareForRelease() {
    doLast {
        def flagged = []
        def pom = new XmlSlurper().parse("$rootDir/bom/imported/pom.xml")

        pom.properties.'*'*.name().each { dep ->
            if (pom.properties."${dep}" =~ "FEATURE") {
                flagged.add("$dep --> ${pom.properties."${dep}"}")
            }
        }

        if (!flagged.isEmpty()) {
            throw new org.gradle.api.GradleException("Found FEATURE versions while preparing for release, please " +
                    "release the following components and update the project dependencies in bom/imported/pom.xml: \n\n" +
                    "${flagged.join("\n")}"
            )
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}

apply from: "$rootDir/gradle/bom.gradle"

defaultTasks "installBillOfMaterials", "clean", "build"
